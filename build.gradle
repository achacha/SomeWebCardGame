
import org.flywaydb.gradle.task.FlywayCleanTask
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.postgresql:postgresql:${postgresVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.2.0'
    }
}

plugins {
    id 'com.moowork.node' version '1.3.1'
    // Flyway version also needs to be updated in gradle.properties
    id 'org.flywaydb.flyway' version '6.5.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "org.owasp.dependencycheck" version '5.3.2.1'
}

group 'org.achacha'
version '1.0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.junit.platform.gradle.plugin'

/*
Load properties file from user home directory
 */
def homeProperties = new Properties()
file("${System.properties['user.home']}${File.separator}.sawcog.properties").withReader { homeProperties.load(it) }

//sourceSets['main'].allSource.add("webapp" as FileCollection)

apply from: 'dependencies.gradle'

// IntelliJ settings
idea {
    module {
        name = 'SomeWebCardGame'
        inheritOutputDirs = true

        sourceDirs += file("src/main/generated")

        testSourceDirs += file("src/test/generated_tests")
        testSourceDirs += file("src/test/integration")

        generatedSourceDirs += file("src/main/generated")
        generatedSourceDirs += file("src/test/generated_tests")
    }
}

task ideaInfo {
    group 'help'
    description 'IntelliJ Idea info'
    println "idea.module.sourceDirs="+idea.module.sourceDirs
    println "idea.module.testSourceDirs="+idea.module.testSourceDirs
    println "idea.module.generatedSourceDirs="+idea.module.generatedSourceDirs
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

war {
    manifest {
        attributes 'Implementation-Title': 'Some Web Card Game',
                'Implementation-Vendor': 'Alex Chachanashvili on ' + java.time.LocalDateTime.now(),
                'Implementation-Version': version
    }
}

println "MODE=${homeProperties['mode']}"

//noinspection GroovyUnusedAssignment
sourceCompatibility = 11
//noinspection GroovyUnusedAssignment
targetCompatibility = 11

repositories {
    mavenCentral()
}

test {
    // Use JUnit 5
    useJUnitPlatform()

    maxHeapSize = '1G'
}

task copyJs(type: Copy, dependsOn: [ 'cleanJs', 'npmSetup', 'nodeSetup', 'yarnInstall' ] ) {
    group 'build'
    description 'Copy JavaScript dependencies to webapp'
    from 'node_modules'
    include '*/dist/**/*'
    include '*/build/*'
    include 'font-awesome/css/*'
    include 'font-awesome/fonts/*'
    include 'requirejs/require.js'
    into "${projectDir}/src/main/webapp/js"
    includeEmptyDirs = false
}

task cleanJs(type: Delete) {
    group 'build'
    description 'Remove JavaScript from webapp'
    delete "${projectDir}/src/main/webapp/js"
    delete "${projectDir}/package-lock.json"
    delete "${projectDir}/yarn.lock"
}

war.dependsOn copyJs
clean.dependsOn cleanJs

//
// Flyway tasks
//
task flywayMainClean(type: FlywayCleanTask) {
    description 'Clean database'
    description 'Migrate database and populate data'
    url = "${homeProperties['flyway.db.jdbcUrl']}"
    driver = "${homeProperties['flyway.db.driverClassName']}"
    user = "${homeProperties['flyway.db.username']}"
    password = "${homeProperties['flyway.db.password']}"
    locations = [
            'filesystem:./db/migration'
    ]
}

task flywayMainMigrate(type: FlywayMigrateTask) {
    description 'Create tables and initialize database'
    url = "${homeProperties['flyway.db.jdbcUrl']}"
    driver = "${homeProperties['flyway.db.driverClassName']}"
    user = "${homeProperties['flyway.db.username']}"
    password = "${homeProperties['flyway.db.password']}"
    locations = [
            'filesystem:./db/migration'
    ]
}

task flywayTestClean(type: FlywayCleanTask) {
    description 'Clean Test database'
    url = "${homeProperties['flyway.test.db.jdbcUrl']}"
    driver = "${homeProperties['flyway.test.db.driverClassName']}"
    user = "${homeProperties['flyway.test.db.username']}"
    password = "${homeProperties['flyway.test.db.password']}"
    locations = [
            'filesystem:./db/migration',
            'filesystem:./db/test'
    ]
}

task flywayTestMigrate(type: FlywayMigrateTask) {
    description 'Populate test data'
    url = "${homeProperties['flyway.test.db.jdbcUrl']}"
    driver = "${homeProperties['flyway.test.db.driverClassName']}"
    user = "${homeProperties['flyway.test.db.username']}"
    password = "${homeProperties['flyway.test.db.password']}"
    locations = [
            'filesystem:./db/migration',
            'filesystem:./db/test'
    ]
}

//
// Yarn Tasks
//
task yarnInstall(type: YarnTask, dependsOn: yarnSetup) {
    group 'node'
    description 'Yarn install'

    args = ['install']
}
task yarnOutdated(type: YarnTask, dependsOn: yarnInstall) {
    group 'node'
    description 'Report on outdated dependencies'

    args = ['outdated']
}
task yarnUpgrade(type: YarnTask, dependsOn: yarnInstall) {
    group 'node'
    description 'Upgrade outdated dependencies to their latest versions'

    args = ['upgrade']
}
